grammar dk.sdu.mmmi.sse02.tutorial2.EntityLanguage with org.eclipse.xtext.common.Terminals

generate entityLanguage "http://www.sdu.dk/mmmi/sse02/tutorial2/EntityLanguage"

Domainmodel:
  (elements += AbstractElement)*
;

PackageDeclaration:
  	'package' name = QualifiedName '{' 
  		(elements += AbstractElement)*
  	'}'
;

AbstractElement:
	PackageDeclaration | Type | Import
;

QualifiedName:
	ID ('.' ID)*
;


Import:
	'import' importedNamespace = QualifiedNameWithWildcard
;

QualifiedNameWithWildcard:
	QualifiedName '.*'?
;

Type:
	DataType | Entity
;

DataType:
	'datatype' name = ID
;

Entity:
	'entity' name = ID 
	    ('extends' supertype = [Entity | QualifiedName])? 
	'{'
		(features += Feature)*
	'}'
;

Feature:
	(many ?= 'many')? name = ID ':' type = [Type | QualifiedName]
;
