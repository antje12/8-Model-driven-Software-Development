grammar dk.sdu.mmmi.mdsd.Math with org.eclipse.xtext.common.Terminals

generate math "http://www.sdu.dk/mmmi/mdsd/Math"

MathExp:
	variables+=Variable*;

Declaration:
	Variable | Let;

Variable returns Declaration:
	{Var} 'var' name=ID '=' value=Exp;

Let returns Declaration:
	{Let} 'let' name=ID '=' value=Exp 'in' in=Exp 'end';

Exp:
	PlusMinus;

PlusMinus returns Exp:
	MultDiv ({PlusMinus.left=current} operator=OpPlusMinus right=MultDiv)*;

MultDiv returns Exp:
	Primary ({MultDiv.left=current} operator=OpMultDiv right=Primary)*;

Primary returns Exp:
	Parenthesis | Number | Let | VariableUse;

OpPlusMinus:
	{Plus} '+' | {Minus} '-';

OpMultDiv:
	{Mult} '*' | {Div} '/';

Number:
	{Number} value=INT;

Parenthesis:
	{Parenthesis} '(' exp=Exp ')';

VariableUse:
	{VariableUse} ref=[Declaration];
