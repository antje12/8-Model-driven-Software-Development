grammar org.xtext.example.mydsl.MyDsl with org.eclipse.xtext.common.Terminals

generate myDsl "http://www.xtext.org/example/mydsl/MyDsl"

System:
    'system' name=ID externals+=ExternalDefinitions* (elements+=(Relation|Entity))+ 
;

ExternalDefinitions:
	'external' name=ID '('(types+=Type (',' types+=Type)*)? ')'
;

Entity:
    'entity' name=ID (elements+=(Attribute|Require))*
;

Attribute:
    'attribute' name=ID 'as' type=Type
;

Require:
    'require' require=(FunCall | VarExp) 
;

VarExp: 
	varRef=[Attribute] '==' exp=INT
;

FunCall: 
	function=[ExternalDefinitions] '('(variables+=[Attribute] (',' variables+=[Attribute])*)? ')'
;

Type:
    'string' | 'number'
;

Relation:
    {Inheritance} 'relation' baseEntity=[Entity] 'is' superEntity=[Entity] |
    {Association} 'relation' ('a'|manyFrom?='many')? from=[Entity] ('has'|'have') ('a'|manyTo?='many')? to=[Entity]
;
